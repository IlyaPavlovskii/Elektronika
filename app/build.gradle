plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'com.github.triplet.play'
}

android {
    namespace 'by.bulba.watch.elektronika'
    compileSdk 33

    defaultConfig {
        applicationId "by.bulba.watch.elektronika"
        minSdk 28
        targetSdk 33
        versionCode 6
        versionName "1.0.5"
    }

    buildFeatures {
        viewBinding true
    }

    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("config/elektronika.properties")
            def properties = new Properties()
            properties.load(new FileInputStream(keystorePropertiesFile))

            storeFile file(properties["store.path"])
            keyAlias = properties["key.alias"]
            storePassword properties["store.password"]
            keyPassword properties["key.password"]
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/atomicfu.kotlin_module']
        }
    }


    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation libs.androidx.core.ktx
    implementation libs.kotlinx.coroutines.core
    implementation libs.kotlinx.coroutines.android
    implementation libs.androidx.activity.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.lifecycle.viewmodel.ktx
    implementation libs.androidx.navigation.fragment.ktx
    implementation libs.androidx.navigation.ui.ktx

    implementation libs.android.material

    implementation libs.androidx.wear
    implementation libs.wear.watchface.complications.data
    implementation libs.wear.watchface.complications.data.source

    implementation libs.wear.watchface
    implementation libs.wear.watchface.client
    implementation libs.wear.watchface.complications.rendering
    implementation libs.wear.watchface.data
    implementation libs.wear.watchface.editor
    implementation libs.wear.watchface.style
    implementation libs.tinkoff.scrollingpagerindicator
}

play {
    serviceAccountCredentials.set(file("../config/elektronika-service-account.json"))
    track.set("internal")
    userFraction.set(0.99D)
    updatePriority.set(1) // in-app update priority [0-5]
    releaseStatus.set(com.github.triplet.gradle.androidpublisher.ReleaseStatus.IN_PROGRESS)
    defaultToAppBundles.set(true)
    //artifactDir.set(file("build/artifacts"))
}